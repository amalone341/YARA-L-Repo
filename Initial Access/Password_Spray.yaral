rule Password_spray_attempt {

  meta:
      // application will be the same for all logs
      // License: https://github.com/amalone341/YARA-L-Work/blob/main/LICENSE
    author = "amalone"
    description = "This detection looks for login failures from more than 25 users where there were more than double the users with failures than successes in 10 minutes"
    severity = "Medium"

  events:
      // failed logins
    $fail.metadata.event_type = "USER_LOGIN" 
    $fail.security_result.summary = /Fail/ nocase 
    
    $fail.target.application = $app
    $fail.metadata.event_timestamp.seconds = $times_fail // total fails
    $fail.target.user.userid = $users_failed // variable to hold failed login usernames


      // successful logins
    $success.metadata.event_type = "USER_LOGIN"
    $success.security_result.summary = /Success/
    
      // Join over the application
    $success.target.application = $app
    
    $success.metadata.event_timestamp.seconds = $times_success // total successes
    $success.target.user.userid = $users_success // variable to hold successful login usernames
    
    

  match:
     $app over 10m

  outcome:
  // add these variables in the columns for context later
     $Distinct_users_failed = count_distinct($fail.target.user.userid) // count distinct usernames associated with fails
     $Distinct_users_succeed = count_distinct($success.target.user.userid) // count distinct usernames associated with successes

  condition:
      // To Edit the threshhold modify the three numbers. The variables are:
      //.   fail: the event that shows there has to be a failed login
      //.   success: the event that shows there has to be a successful 
      //.   times_fail: total failed logins
      //.   times_success: total successful logins
      //.   users_failed: number of unique users that failed a login
      //.   users_success: number of unique users with a successful login
      //
      // Can't do "number of fails is more than double the successes" but this gets close to that logic
      // Bar starts at 11 and 24 to mimic at least 25 failed logins 
      // There can be cases where #user_success > #user_failed * .5
    $fail and $success and #times_fail > #times_success and #users_success > 11 and #users_failed > 24 and #users_failed > #users_success
}
