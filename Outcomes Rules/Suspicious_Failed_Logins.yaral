rule AM_Suspicious_Failed_Logins{

  meta:
    // License: https://github.com/amalone341/YARA-L-Work/blob/main/LICENSE
    author = "Andy Malone"
    status = "Testing"
    description = "Uses the outcomes section to look for a large number of IPs trying to log into a single username"
    severity = "Medium"

  events:

   // Match on the username people will login to
   $e.metadata.event_type = "USER_LOGIN" 
   $e.security_result.summary = "Failed login occurred" 

   $user = $e.target.user.userid
   $user = strings.to_lower($user) // NOCASE the user
   $e.metadata.event_timestamp.seconds = $times
   $e.principal.ip = $pip
   $e.principal.location.country_or_region = $regions
 


  match:
     $user over 2d

  outcome:
    $Total_Fails = count($e.target.user.userid) // count of total failed logins
    $Distinct_Src_IP = count_distinct($e.principal.ip) // Distinct IPs accosiated with login attempts
    $Country_distinct_count = count_distinct($e.principal.location.country_or_region) // Distinct countries with UID
    $Uniq_List_IP = array_distinct($e.principal.ip) 
     

  condition:
      // To Edit the threshhold modify the three numbers. The variables are:
      //.   pip: number of unique source IP addresses attempting to login
      //.   regions: number of unqiue country codes logins have been attempted from
      //.   times: total number of failed logins
    $e and #pip > 3 and #regions > 3 and #times > 30
}
